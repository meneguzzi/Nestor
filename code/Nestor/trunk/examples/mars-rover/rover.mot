Motivation keepBattery {
	Threshold = 10;
	
	IntensityUpdate org.kcl.nestor.mot.impl.IntensityUpdateFunctionImpl {
		//Will need something like
		at(X,Y) [source(percept)] 
		& chargeStation(Cx,Cy) 
		& rover.act.distance(X,Y,Cx,Cy,Dist) 
		& battery(Batt) [source(percept)]
		& (Dist >= Batt) -> 100; // If we detect we are too far from the charge station, go there
		
		battery(Batt) [source(percept)]
		& at(X,Y)
		& not moving
		& waypoint(Wx,Wy) 
		& rover.act.distance(X,Y,Wx,Wy,DistW)
		& DistW >= Batt -> 10; // Similar condition to above, but now we want to charge before going
	}
	
	GoalGeneration org.kcl.nestor.mot.impl.GoalGenerationFunctionImpl {
		chargeStation(Cx,Cy) -> +!recharge(Cx,Cy);
		//waypoint(X,Y) -> -!goToWaypoint(X,Y);
	}
	
	Mitigation org.kcl.nestor.mot.impl.MitigationFunctionImpl {
		battery(40) -> -1000;
	}
}

Motivation navigate {
	Threshold = 10;
	IntensityUpdate org.kcl.nestor.mot.impl.IntensityUpdateFunctionImpl {
		waypoint(Wx,Wy) [source(self)] & not charging -> 5; // If there's a waypoint, we want to go there
		
		battery(Batt) [source(percept)]
		& at(X,Y)
		& waypoint(Wx,Wy) 
		& rover.act.distance(X,Y,Wx,Wy,Dist)
		& Dist >= Batt -> -10; // But if we don't have enough battery to do, we'd better stay
	}
	
	GoalGeneration org.kcl.nestor.mot.impl.GoalGenerationFunctionImpl {
		not moving & not charging & waypoint(X,Y) [source(self)] -> +!goToWaypoint(waypoint(X,Y));
		//at(X,Y) & waypoint(X,Y) -> -waypoint(X,Y); // to avoid adopting an already achieved goal
	}
	
	Mitigation org.kcl.nestor.mot.impl.MitigationFunctionImpl {
		not moving & //not moving is to ensure that the plan has been completed before mitigating
		at(X,Y) & 
		waypoint(X,Y) -> -1000;
	}
}

Motivation finish {
	Threshold = 10;
	
	IntensityUpdate org.kcl.nestor.mot.impl.IntensityUpdateFunctionImpl {
		not waypoint(_,_) [source(self)] & endSimulation [source(self)] -> 10;
	}
	
	GoalGeneration org.kcl.nestor.mot.impl.GoalGenerationFunctionImpl {
		endSimulation [source(self)] -> +!endSimulation;
	}
	
	Mitigation org.kcl.nestor.mot.impl.MitigationFunctionImpl {
		//true -> -100;
	}
}